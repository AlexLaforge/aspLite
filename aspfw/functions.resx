<%
'here come some very useful functions 

function isNumber(byval value)

	if isLeeg(value) then
		isNumber=false
	else
		isNumber=isNumeric(value)
	end if
	
end function


Function isLeeg(byval value)
	
	isLeeg=false
	
	if isNull(value) then
		isLeeg=true
	else
		if isEmpty(value) or trim(value)="" then isLeeg=true
	end if
	
End Function


Function convertStr(value)

	on error resume next
	
	if not isnull(value) then
		convertStr=cstr(value)
	else
		convertStr=""
	end if
	
	if err.number<>0 then
		convertStr=value
	end if
	
	on error goto 0
	
End Function

function sanitize(sValue)

	if isLeeg(sValue) then
		sanitize=sValue
	else
		sanitize=replace(sValue,"""","&quot;",1,-1,1)
		sanitize=replace(sanitize,"<","&lt;",1,-1,1)
		sanitize=replace(sanitize,">","&gt;",1,-1,1)
	end if
	
end function


function sanitizeJS(sValue)

	sanitizeJS=replace(sValue,"'","\'",1,-1,1)
	
end function


Function convertGetal(value)

	on error resume next

	if isNumber(value) then 
		convertGetal=cdbl(value)
	else
		convertGetal=0
	end if
	
	if err.number<>0 then convertGetal=0
	
	on error goto 0
				
End Function

Function convertBool(value)
	
	On Error Resume Next
	
	if isLeeg(value) then
		convertBool=false
		exit function
	end if
	if convertGetal(value)=1 then
		convertBool=true
		exit function
	end if
	if cstr(value)="0" then
		convertBool=false
		exit function
	end if
	if lcase(cstr(value))="true" then
		convertBool=true
		exit function
	end if
	if lcase(cstr(value))="false" then
		convertBool=false
		exit function
	end if
	if value=true or cBool(value) then
		convertBool=true
		exit function
	end if
	
	convertBool=false
	
	On Error Goto 0
	
End Function

function sqli(str)
	if isLeeg(str) then
		sqli=""
	else
		sqli=replace(str,"'","''",1,-1,1)
	end if
end function


function numberList(startNr,stopNr,interval,selected)
	dim i
	for i=startNr to stopNr step interval
		numberList=numberList& "<option value=" & i 
		if convertGetal(selected)=convertGetal(i) then numberList=numberList & " selected"
		numberList=numberList& ">" & i & "</option>"
	next
end function

Function randomText(nmbrChars)

	dim i
	for i=1 to nmbrChars	
		randomText=randomText & CHR(Int((122-97+1)*Rnd+97))
	next
   
End Function

function randomNumber(startnr,stopnr)

	randomNumber=Int((stopnr-startnr+1)*Rnd+startnr)
	
end function

function convert2 (byref getal)
	if len(getal)=1 then 
		convert2="0"&getal
	else
		convert2=getal
	end if
end function

%>