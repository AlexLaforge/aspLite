<%
'load the template file first (including JavScript CDN)
body=aspL.load("html/demo.asp/jszip.resx")	

'this folder will be zipped by the browser!
dim folderToZip
folderToZip="uploads"

'get filelist
dim filelist,filelistArr,objSuperFolder,filepath,filetype
set filelist=server.createobject("scripting.dictionary")
set objSuperFolder = aspL.fso.getfolder(server.mappath(folderToZip))

ShowSubfolders (objSuperFolder)

set objSuperFolder=nothing

dim url
for each url in filelist
	filelistArr=filelistArr & vbtab & "urls.push(['" & aspL.sanitizeJS(url) & "','" & filelist(url) & "'])" & vbcrlf
next

body=replace(body,"[filelist]",filelistArr,1,-1,1)

set filelist=nothing

Sub ShowSubFolders(fFolder)

	dim objFolder,colFiles,Subfolder

	Set objFolder = aspL.fso.GetFolder(fFolder.Path)
	Set colFiles = objFolder.Files	
	
	For Each objFile in colFiles
		
		if instr(objFile,".")<>0 then
			
			filepath=objFile.path
			filepath=replace(filepath,server.mappath(folderToZip),folderToZip,1,-1,1)			
			filepath=replace(filepath,"\","/",1,-1,1)			

			filetype=lcase(aspL.getFileType(objFile.name))
			
			if aspL.safeFileTypes.exists(filetype) then
			
				select case filetype								
					
					case "json"
						filelist.add filepath,"json"
						
					case "xml","svg"
						filelist.add filepath,"document"	
					
					case "htm","html","css","txt","csv","js"
						filelist.add filepath,"text"
						
					case "mdb"
						'we're not including these files for security reasons
				
					case else				
						filelist.add filepath,"blob"		
				
				end select			
			
			end if
			
		end if
	Next

	For Each Subfolder in fFolder.SubFolders
		ShowSubFolders(Subfolder)
	Next
	
	set objFolder=nothing
	set colFiles=nothing
	
End Sub

%>